TTLOG
 Q

CHOOSE(CHOSEN)
 N MA,LOGNUM,LOGIDX,ITEMTITLE,DEFTITLE,DEFLOG S LOGIDX=""
 S MA("TITLE")="LOG"
 S LOGIDX=$O(^TT("LOG",LOGIDX))
 ;S DEFTITLE=$ZDATE(^TT("LOG",LOGIDX,"START"))_" "_^TT("CUSTOMER",^TT("LOG",LOGIDX,"CUSTCODE"))
 ;S MA("DEFAULT")=DEFTITLE
 S LOGIDX=""
 F  S LOGIDX=$O(^TT("LOG",LOGIDX)) Q:LOGIDX=""  D
 . I ^TT("LOG",LOGIDX,"STOP")="" D
 . . S ITEMTITLE=$ZDATE(^TT("LOG",LOGIDX,"START"))_" "_^TT("CUSTOMER",^TT("LOG",LOGIDX,"CUSTCODE"))
 . . S MA("ITEMS",ITEMTITLE)="S CURRENTLOG="_LOGIDX
 D GO^TTMENU(.MA)
 M CHOSEN=^TT("LOG",CURRENTLOG)
 Q CURRENTLOG

START
 N FA
 S FA("TITLE")="START TIME LOG"
 S FA("ITEMS",1,"NAME")="TASK"
 S FA("ITEMS",1,"LABEL")="TASK PERFORMED:"
 S FA("ITEMS",1,"LABEL","TOP")=4
 S FA("ITEMS",1,"LABEL","LEFT")=24
 S FA("ITEMS",1,"FIELD","TOP")=4
 S FA("ITEMS",1,"FIELD","LEFT")=40
 S FA("ITEMS",1,"FIELD","MAXLENGTH")=35
 S FA("ITEMS",1,"FIELD","REQUIRED")=1
 S FA("ITEMS",2,"NAME")="CUSTOMER"
 S FA("ITEMS",2,"LABEL")="CUSTOMER:"
 S FA("ITEMS",2,"LABEL","TOP")=6
 S FA("ITEMS",2,"LABEL","LEFT")=30
 S FA("ITEMS",2,"FIELD","TOP")=6
 S FA("ITEMS",2,"FIELD","LEFT")=40
 S FA("ITEMS",2,"FIELD","MAXLENGTH")=30
 S FA("ITEMS",2,"FIELD","REQUIRED")=1
 S FA("ITEMS",2,"FIELD","ONFOCUS")="S I=$$CHOOSE^TTCUST(.B)"
 D GO^TTFORM(.FA)
 G:CANFLG=1 CANCELSTART
 N CUSTCODE,TASK,LOGRES,CUSTREC
 S CUSTCODE=$$GETINTVAL^TTFORM(.FA,"CUSTOMER")
 S TASK=$$GETEXTVAL^TTFORM(.FA,"TASK")
 S LOGRES=$$CREATE(CUSTCODE,TASK)
CANCELSTART
 Q
 
STOP
 N LOGIDX,LOGREC,ENTRIDX,HRS,CUSTCODE,START,STOP,TASK
 S LOGIDX=$$CHOOSE(.LOGREC)
 I $G(^TT("LOG",LOGIDX,"START"))="" W !,"INVALID LOG NUMBER",! G STOP
 S CUSTCODE=^TT("LOG",LOGIDX,"CUSTCODE")
 S ENTRIDX=^TT("CUSTOMER",CUSTCODE,"ENTRIDX")
 S START=^TT("LOG",LOGIDX,"START")
 S TASK=^TT("LOG",LOGIDX,"TASK")
 S ^TT("LOG",LOGIDX,"STOP")=$H
 S STOP=^TT("LOG",LOGIDX,"STOP")
 S ^TT("ENTR",CUSTCODE,ENTRIDX,"HOURS")=$$HOURS^TTDATE(START,STOP)
 S ^TT("ENTR",CUSTCODE,ENTRIDX,"DATE")=$ZDATE(START)
 S ^TT("ENTR",CUSTCODE,ENTRIDX,"TASK")=TASK
 S ^TT("ENTR",CUSTCODE,ENTRIDX,"BILLED")="NO"
 S ^TT("ENTR",CUSTCODE,ENTRIDX,"CUSTCODE")=CUSTCODE
 S ^TT("CUSTOMER",CUSTCODE,"ENTRIDX")=^TT("CUSTOMER",CUSTCODE,"ENTRIDX")+1
 Q
 
CREATE(CUSTCODE,TASK)
 I $G(^TT("CUSTOMER",CUSTCODE))="" Q "ERROR"
 N IDX S IDX=^TT("COMPANY","LOGIDX")
 S ^TT("LOG",IDX,"TASK")=TASK,^TT("LOG",IDX,"CUSTCODE")=CUSTCODE
 S ^TT("LOG",IDX,"START")=$H,^TT("LOG",IDX,"STOP")=""
 S ^TT("COMPANY","LOGIDX")=^TT("COMPANY","LOGIDX")+1
 Q IDX
 
LIST
 N LOGIDX,START,HOURS S LOGIDX=""
 N EARNED,CUSTCODE,FILE
 D OPENTEMP^TTIO(.FILE)
 W !,"OPEN LOGS",!,!,$J("NUMBER",6),$J("CUSTOMER",20),$J("HOURS",20),$J("EARNED",20),!
 F I=1:1:76 W "-"
 W !
 F  S LOGIDX=$O(^TT("LOG",LOGIDX)) Q:LOGIDX=""  D
 . I ^TT("LOG",LOGIDX,"STOP")="" D
 . . S START=^TT("LOG",LOGIDX,"START")
 . . S HOURS=$$HOURS^TTDATE(START,$H)
 . . S CUSTCODE=^TT("LOG",LOGIDX,"CUSTCODE")
 . . S EARNED=HOURS*^TT("CUSTOMER",CUSTCODE,"RATEHOUR")
 . . W $J(LOGIDX,6),$J(^TT("LOG",LOGIDX,"CUSTCODE"),20),$J(HOURS,20,2),$J(EARNED,20,2),!,"             ",^TT("LOG",LOGIDX,"TASK"),!
 D CLOSETEMP^TTIO(.FILE)
 D VIEW^TTFILE(FILE,"OPEN LOGS")
 Q