TTACCOUNT
 N MA
TOP
 S MA("TITLE")="GENERAL LEDGER SYSTEM"
 S MA("ITEMS","ADD ACCOUNT")="D ADD^TTACCOUNT"
 S MA("ITEMS","JOURNAL")="D INITJOURNAL^TTACCOUNT"
 S MA("ITEMS","BALANCE SHEET")="D INITBALSHEET^TTACCOUNT"
 S MA("ITEMS","TRIAL BALANCE")="D TRIALBAL^TTACCOUNT"
 S MA("ITEMS","ADD NEW ACCOUNTING PERIOD")="D ADDPERIOD^TTACCOUNT"
 S MA("ITEMS","SET CURRENT ACCOUNTING PERIOD")="D SETCURRENTPERIOD^TTACCOUNT"
 S MA("ITEMS","POST MANUAL TRANSACTION")="D INITPOST^TTACCOUNT"
 S MA("ITEMS","CHART OF ACCOUNTS")="D COA^TTACCOUNT"
 S MA("ITEMS","INCOME STATEMENT")="D INITINCOMESTMT^TTACCOUNT"
 S MA("ITEMS","QUIT TO MAIN MENU")="D ^TT"
 D GO^TTMENU(.MA)
 G TOP
 Q

COA
 N ANUM,FILE S ANUM=""
 D OPENTEMP^TTIO(.FILE)
 W !,"CHART OF ACCOUNTS"
 W !,"=================",!,!
 W "ACCT",?8,"DESCRIPTION",?49,"TYPE",?69,"BALANCE",!
 F I=1:1:80 W "-"
 W !
 F  S ANUM=$O(^TT("ACCT",ANUM)) Q:ANUM=""  D
 . W ANUM,?8
 . W $E(^TT("ACCT",ANUM),1,39),?49
 . W $E(^TT("ACCT",ANUM,"TYPE"),1,29),?69
 . W "$",$J($$BALANCE^TTACCOUNT(ANUM,^TT("COMPANY","CURRENTPERIOD")),10,2),!
 D CLOSETEMP^TTIO(.FILE)
 D VIEW^TTFILE(FILE,"CHART OF ACCOUNTS")
 Q

SETUP
 I $G(^TT("COMPANY","JSEQ"))="" S ^TT("COMPANY","JSEQ")=100
 S ^TT("ALKP","ASSET","CREDIT")="-"
 S ^TT("ALKP","ASSET","DEBIT")="+"
 S ^TT("ALKP","LIABILITY","CREDIT")="+"
 S ^TT("ALKP","LIABILITY","DEBIT")="-"
 S ^TT("ALKP","REVENUE","CREDIT")="+"
 S ^TT("ALKP","REVENUE","DEBIT")="-"
 S ^TT("ALKP","EXPENSE","CREDIT")="-"
 S ^TT("ALKP","EXPENSE","DEBIT")="+"
 S ^TT("ALKP","EQUITY","CREDIT")="+"
 S ^TT("ALKP","EQUITY","DEBIT")="-"
 Q

INITINCOMESTMT
 N FROM,TO,FILE
 W !,"RESPOND FROM DATE:"
 S FROM=$$CHOOSE^TTDATE
 W !,"RESPOND TO DATE:"
 S TO=$$CHOOSE^TTDATE
 D OPENTEMP^TTIO(.FILE)
 D INCOMESTMT^TTACCOUNT(FROM,TO)
 D CLOSETEMP^TTIO(.FILE)
 D VIEW^TTFILE(FILE,"INCOME STATEMENT")
 Q

INCOMESTMT(FROMDATE,TODATE)
 N REVACCTS,EXPACCTS,CURPERIOD
 N TOTREV,TOTEXP,ANUM S (TOTREV,TOTEXP)=0,ANUM=""
 S CURPERIOD=^TT("COMPANY","CURRENTPERIOD")
 D BYTYPE("REVENUE",.REVACCTS)
 D BYTYPE("EXPENSE",.EXPACCTS)
 W !,"INCOME STATEMENT",!
 W "FROM ",FROMDATE," TO ",TODATE,!
 F I=1:1:80 W "-"
 W !,!
 W "REVENUES & GAINS",!
 W "----------------",!
 N TBAL
 F  S ANUM=$O(REVACCTS(ANUM)) Q:ANUM=""  D
 . S TBAL=$$BALANCE(ANUM,CURPERIOD)
 . S TOTREV=TOTREV+TBAL
 . W ?2,^TT("ACCT",ANUM),?30,"$",$J(TBAL,10,2),!
 W ?4,"TOTAL REVENUES",?30,"$",$J(TOTREV,10,2)
 W !,!
 W "EXPENSES & LOSSES",!
 W "-----------------",!
 N TBAL
 F  S ANUM=$O(EXPACCTS(ANUM)) Q:ANUM=""  D
 . S TBAL=$$BALANCE(ANUM,CURPERIOD)
 . S TOTEXP=TOTEXP+TBAL
 . W ?2,^TT("ACCT",ANUM),?30,"$",$J(TBAL,10,2),!
 W ?4,"TOTAL EXPENSES",?30,"$",$J(TOTEXP,10,2),!,!
 W "NET INCOME",?30,"$",$J(TOTREV-TOTEXP,10,2),!,!
 Q


ADD
 N NUM,NAME,TYPE,PERS
 W !,"RESPOND ACCOUNT NUMBER "
 R NUM
 W !,"RESPOND ACCOUNT NAME "
 R NAME
 N MA S MA("TITLE")="ACCOUNT TYPE"
 S MA("ITEMS","ASSET")="S TYPE="""_"ASSET"_""""
 S MA("ITEMS","EQUITY")="S TYPE="""_"EQUITY"_""""
 S MA("ITEMS","LIABILITY")="S TYPE="""_"LIABILITY"_""""
 S MA("ITEMS","REVENUE/INCOME")="S TYPE="""_"REVENUE"_""""
 S MA("ITEMS","EXPENSE")="S TYPE="""_"EXPENSE"_""""
 D GO^TTMENU(.MA)
 K MA S MA("TITLE")="ACCOUNT PERSISTENCE"
 S MA("ITEMS","TEMPORARY ACCOUNT")="S PERS="""_"TEMPORARY"_""""
 S MA("ITEMS","PERMANENT (REAL) ACCOUNT")="S PERS="""_"PERMANENT"_""""
 D GO^TTMENU(.MA)
 D CREATE^TTACCOUNT(NUM,NAME,TYPE,PERS) 
 Q

CHOOSE(CHOSEN)
 N MA,NUM,CURRENTNUM S NUM=""
 S MA("TITLE")="ACCOUNT"
 F  S NUM=$O(^TT("ACCT",NUM)) Q:NUM=""  D
 . S MA("ITEMS",^TT("ACCT",NUM))="S CURRENTNUM="""_NUM_""""
 D GO^TTMENU(.MA)
 M CHOSEN=^TT("ACCT",CURRENTNUM)
 S INTVAL=CURRENTNUM
 S EXTVAL=^TT("ACCT",CURRENTNUM)
 Q CURRENTNUM

BYTYPE(TYPE,RETURNARR)
 N NUM S NUM=""
 N OA
 F  S NUM=$O(^TT("ACCT",NUM)) Q:NUM=""  D
 . I ^TT("ACCT",NUM,"TYPE")=TYPE S RETURNARR(NUM)=""
 Q

INITJOURNAL
 N ACCOUNT,PERIOD,TMP,FILE
 S ACCOUNT=$$CHOOSE^TTACCOUNT(.TMP)
 S PERIOD=$$CHOOSEPERIOD^TTACCOUNT
 D OPENTEMP^TTIO(.FILE)
 D JOURNAL(FILE,ACCOUNT,PERIOD)
 D CLOSETEMP^TTIO(.FILE)
 D VIEW^TTFILE(FILE,"JOURNAL")
 Q

JOURNAL(FILE,ACCOUNT,PERIOD)
 N SEQ,JNL,JSEQ,DATE,CREDOP,DEBOP S (SEQ,JNL,JSEQ)=""
 N DEBIT,CREDIT,BAL,AMT S (DEBIT,CREDIT,BAL,AMT)=0
 S (CREDIT,DEBIT)=""
 U FILE
 W !,!
 W "SEQ",?7,"DATE",?18,"JOURNAL",?47,"DEBIT",?59,"CREDIT",!
 F I=1:1:80 W "-"
 W !
 F  S SEQ=$O(^TT("ACCT",ACCOUNT,PERIOD,SEQ)) Q:SEQ=""  D
 . S JSEQ=^TT("ACCT",ACCOUNT,PERIOD,SEQ,"JSEQ")
 . S AMT=^TT("ACCT",ACCOUNT,PERIOD,SEQ)
 . S JNL=$E(^TT("JNL",JSEQ),1,27)
 . S DATE=^TT("ACCT",ACCOUNT,PERIOD,SEQ,"DATE")
 . S CREDOP=$$OPER^TTACCOUNT(ACCOUNT,"CREDIT")
 . S DEBOP=$$OPER^TTACCOUNT(ACCOUNT,"DEBIT")
 . W SEQ,?7,DATE,?18,JNL,?47
 . I $$ISDEBIT(DEBOP,AMT)=1 S DEBIT=AMT E  S DEBIT=""
 . I $$ISCREDIT(CREDOP,AMT)=1 S CREDIT=-AMT E  S CREDIT=""
 . W DEBIT,?59,CREDIT,!
 . S (DEBIT,CREDIT)=""
 C FILE
 U $P
 Q

ISCREDIT(CREDOP,AMOUNT)
 N RET S RET=""
 N SIGN S SIGN=""
 I AMOUNT<0 S SIGN="-"
 I AMOUNT>0 S SIGN="+"
 I SIGN=CREDOP S RET=1
 I SIGN'=CREDOP S RET=0
 Q RET

ISDEBIT(DEBOP,AMOUNT)
 N RET S RET=""
 N SIGN S SIGN=""
 I AMOUNT<0 S SIGN="-"
 I AMOUNT>0 S SIGN="+"
 I SIGN=DEBOP S RET=1
 I SIGN'=DEBOP S RET=0
 Q RET

INITBALSHEET
 N PERIOD,FILE S PERIOD=$$CHOOSEPERIOD^TTACCOUNT
 D OPENTEMP^TTIO(.FILE)
 D BALSHEET^TTACCOUNT(FILE,PERIOD)
 D CLOSETEMP^TTIO(.FILE)
 D VIEW^TTFILE(FILE,"BALANCE SHEET")
 Q

BALSHEET(FILE,PERIOD)
 N ASSETS,LIABILITIES,EQUITY,ANUM S ANUM=""
 N TOTASS,TOTLIAB,TOTEQU,TBAL S (TOTASS,TOTLIAB,TOTEQU,TBAL)=0
 U FILE
 D BYTYPE^TTACCOUNT("ASSET",.ASSETS)
 D BYTYPE^TTACCOUNT("LIABILITY",.LIABILITIES)
 D BYTYPE^TTACCOUNT("EQUITY",.EQUITY)
 W ^TT("COMPANY","NAME"),!
 W "BALANCE SHEET AS OF "_$ZD($H),!
 F I=1:1:80 W "-"
 W !,!,!
 W "ASSETS",!
 W "------",!,!
 F  S ANUM=$O(ASSETS(ANUM)) Q:ANUM=""  D
 . N ANAME S ANAME=^TT("ACCT",ANUM)
 . S TBAL=$$BALANCE^TTACCOUNT(ANUM,PERIOD)
 . S TOTASS=TOTASS+TBAL
 . W ?4,ANAME,?60,"$",$J(TBAL,10,2),!
 W ?6,"TOTAL ASSETS",?60,"$",$J(TOTASS,10,2),!,!,!
 W "LIABILITIES",!
 W "-----------",!,!
 F  S ANUM=$O(LIABILITIES(ANUM)) Q:ANUM=""  D
 . N ANAME S ANAME=^TT("ACCT",ANUM)
 . S TBAL=$$BALANCE^TTACCOUNT(ANUM,PERIOD)
 . S TOTLIAB=TOTLIAB+TBAL
 . W ?4,ANAME,?60,"$",$J(TBAL,10,2),!
 W ?6,"TOTAL LIABILITIES",?60,"$",$J(TOTLIAB,10,2),!,!,!
 W "STOCKHOLDERS' EQUITY",!
 W "--------------------",!,!
 N OPEQU S OPEQU=TOTASS-TOTLIAB
 W ?4,"STOCKHOLDERS' EQUITY",?60,"$",$J(OPEQU,10,2),!
 S TOTEQU=OPEQU
 F  S ANUM=$O(EQUITY(ANUM)) Q:ANUM=""  D
 . N ANAME S ANAME=^TT("ACCT",ANUM)
 . S TBAL=$$BALANCE^TTACCOUNT(ANUM,PERIOD)
 . S TOTEQU=TOTEQU+TBAL
 . W ?4,ANAME,?60,"$",$J(TBAL,10,2),!
 ;W ?6,"TOTAL STOCKHOLDERS' EQUITY",?60,"$",$J(TOTEQU,10,2),!,!,!
 W "TOTAL LIABILITIES & STOCKHOLDER'S EQUITY",?60,"$",$J(OPEQU+TOTLIAB,10,2)
 C FILE
 U $P
 Q

BALANCE(ACCOUNT,PERIOD)
 N SEQ S SEQ=""
 N SUM S SUM=0
 F  S SEQ=$O(^TT("ACCT",ACCOUNT,PERIOD,SEQ)) Q:SEQ=""  D
 . S SUM=SUM+^TT("ACCT",ACCOUNT,PERIOD,SEQ)
 Q SUM

CREATE(NUM,NAME,TYPE,PERS)
 TS
 S ^TT("ACCT",NUM)=NAME
 S ^TT("ACCT",NUM,"TYPE")=TYPE
 S ^TT("ACCT",NUM,"SEQ")=100
 S ^TT("ACCT",NUM,"PERSISTENCE")=PERS
 TC
 Q

SETCURRENTPERIOD
 W "SET CURRENT ACCOUNTING PERIOD:",!
 S ^TT("COMPANY","CURRENTPERIOD")=$$CHOOSEPERIOD^TTACCOUNT
 Q

CHOOSEPERIOD()
 N MA,PNAME,CURNAME S PNAME=""
 S MA("TITLE")="ACCOUNTING PERIOD"
 S MA("DEFAULT")=^TT("COMPANY","CURRENTPERIOD")
 F  S PNAME=$O(^TT("ACCTPERIOD",PNAME)) Q:PNAME=""  D
 . S MA("ITEMS",PNAME)="S CURNAME="""_PNAME_""""
 D GO^TTMENU(.MA)
 S EXTVAL=CURNAME
 S INTVAL=CURNAME
 Q CURNAME

ADDPERIOD
 N PNAME
 W !,"RESPOND NEW ACCOUNTING PERIOD: "
 R PNAME
 S ^TT("ACCTPERIOD",PNAME)=""
 Q

INITPOST
 N FA
 S FA("TITLE")="POST MANUAL TRANSACTION"
 S FA("ITEMS",1,"NAME")="AMOUNT"
 S FA("ITEMS",1,"LABEL")="AMOUNT:"
 S FA("ITEMS",1,"LABEL","TOP")=4
 S FA("ITEMS",1,"LABEL","LEFT")=32
 S FA("ITEMS",1,"FIELD","TOP")=4
 S FA("ITEMS",1,"FIELD","LEFT")=40
 S FA("ITEMS",1,"FIELD","MAXLENGTH")=15
 S FA("ITEMS",1,"FIELD","REQUIRED")=1
 S FA("ITEMS",2,"NAME")="DATE"
 S FA("ITEMS",2,"LABEL")="TRANSACTION DATE:"
 S FA("ITEMS",2,"LABEL","TOP")=6
 S FA("ITEMS",2,"LABEL","LEFT")=22
 S FA("ITEMS",2,"FIELD","TOP")=6
 S FA("ITEMS",2,"FIELD","LEFT")=40
 S FA("ITEMS",2,"FIELD","MAXLENGTH")=10
 S FA("ITEMS",2,"FIELD","REQUIRED")=1
 S FA("ITEMS",2,"FIELD","ONFOCUS")="D CHOOSE^TTDATE($ZDATE($H))"
 S FA("ITEMS",3,"NAME")="JOURNAL"
 S FA("ITEMS",3,"LABEL")="JOURNAL:"
 S FA("ITEMS",3,"LABEL","TOP")=8
 S FA("ITEMS",3,"LABEL","LEFT")=31
 S FA("ITEMS",3,"FIELD","TOP")=8
 S FA("ITEMS",3,"FIELD","LEFT")=40
 S FA("ITEMS",3,"FIELD","MAXLENGTH")=30
 S FA("ITEMS",3,"FIELD","REQUIRED")=1
 S FA("ITEMS",4,"NAME")="PERIOD"
 S FA("ITEMS",4,"LABEL")="ACCOUNTING PERIOD: "
 S FA("ITEMS",4,"LABEL","TOP")=10
 S FA("ITEMS",4,"LABEL","LEFT")=21
 S FA("ITEMS",4,"FIELD","TOP")=10
 S FA("ITEMS",4,"FIELD","LEFT")=40
 S FA("ITEMS",4,"FIELD","MAXLENGTH")=4
 S FA("ITEMS",4,"FIELD","REQUIRED")=1
 S FA("ITEMS",4,"FIELD","ONFOCUS")="S I=$$CHOOSEPERIOD^TTACCOUNT" 
 S FA("ITEMS",5,"NAME")="CREDIT"
 S FA("ITEMS",5,"LABEL")="CREDIT ACCOUNT:"
 S FA("ITEMS",5,"LABEL","TOP")=14
 S FA("ITEMS",5,"LABEL","LEFT")=24
 S FA("ITEMS",5,"FIELD","TOP")=14
 S FA("ITEMS",5,"FIELD","LEFT")=40
 S FA("ITEMS",5,"FIELD","MAXLENGTH")=30
 S FA("ITEMS",5,"FIELD","REQUIRED")=1
 S FA("ITEMS",5,"FIELD","ONFOCUS")="S I=$$CHOOSE^TTACCOUNT(.B)"
 S FA("ITEMS",6,"NAME")="DEBIT"
 S FA("ITEMS",6,"LABEL")="DEBIT ACCOUNT:"
 S FA("ITEMS",6,"LABEL","TOP")=16
 S FA("ITEMS",6,"LABEL","LEFT")=25
 S FA("ITEMS",6,"FIELD","TOP")=16
 S FA("ITEMS",6,"FIELD","LEFT")=40
 S FA("ITEMS",6,"FIELD","MAXLENGTH")=30
 S FA("ITEMS",6,"FIELD","REQUIRED")=1
 S FA("ITEMS",6,"FIELD","ONFOCUS")="S I=$$CHOOSE^TTACCOUNT(.B)"
 D GO^TTFORM(.FA)
 G:CANFLG=1 CANCELPOST
 N CREDIT,DEBIT,DATE,AMT,JNL,PERIOD
 S DATE=$$GETEXTVAL^TTFORM(.FA,"DATE")
 S AMT=$$GETEXTVAL^TTFORM(.FA,"AMOUNT")
 S CREDIT=$$GETINTVAL^TTFORM(.FA,"CREDIT")
 S DEBIT=$$GETINTVAL^TTFORM(.FA,"DEBIT")
 S PERIOD=$$GETEXTVAL^TTFORM(.FA,"PERIOD")
 S JNL=$$GETEXTVAL^TTFORM(.FA,"JOURNAL")
 D POST^TTACCOUNT(DATE,AMT,CREDIT,DEBIT,PERIOD,JNL)
CANCELPOST
 Q

OPER(ACCOUNT,ACTION)
 N ATYPE
 S ATYPE=^TT("ACCT",ACCOUNT,"TYPE")
 Q ^TT("ALKP",ATYPE,ACTION) 

POST(DATE,AMOUNT,CREDIT,DEBIT,PERIOD,JOURNAL)
 N SSEQ S SSEQ=^TT("ACCT",CREDIT,"SEQ")
 N DSEQ S DSEQ=^TT("ACCT",DEBIT,"SEQ")
 N JSEQ S JSEQ=^TT("COMPANY","JSEQ")
 TS
 ; CREATE THE DOUBLE ENTRIES
 I $$OPER(CREDIT,"CREDIT")="-" S ^TT("ACCT",CREDIT,PERIOD,SSEQ)=-AMOUNT
 I $$OPER(CREDIT,"CREDIT")="+" S ^TT("ACCT",CREDIT,PERIOD,SSEQ)=AMOUNT
 I $$OPER(DEBIT,"DEBIT")="+" S ^TT("ACCT",DEBIT,PERIOD,DSEQ)=AMOUNT
 I $$OPER(DEBIT,"DEBIT")="-" S ^TT("ACCT",DEBIT,PERIOD,DSEQ)=-AMOUNT
 S ^TT("ACCT",CREDIT,PERIOD,SSEQ,"DATE")=DATE
 S ^TT("ACCT",DEBIT,PERIOD,DSEQ,"DATE")=DATE
 S ^TT("ACCT",CREDIT,PERIOD,SSEQ,"JSEQ")=JSEQ
 S ^TT("ACCT",DEBIT,PERIOD,DSEQ,"JSEQ")=JSEQ
 S ^TT("JNL",JSEQ)=JOURNAL
 S ^TT("JNL",JSEQ,"DATE")=DATE
 S ^TT("COMPANY","JSEQ")=JSEQ+1
 S ^TT("ACCT",CREDIT,"SEQ")=SSEQ+1
 S ^TT("ACCT",DEBIT,"SEQ")=DSEQ+1
 TC
 Q
 