TTCUST
 N MA
TOP
 S MA("TITLE")="CUSTOMER MANAGEMENT"
 S MA("ITEMS","ADD CUSTOMER")="D ADD^TTCUST"
 S MA("ITEMS","SHOW CUSTOMER")="D SHOW^TTCUST"
 S MA("ITEMS","INTEGRATE CUSTOMER TO ACCOUNTING MODULE")="D INTGACCT^TTCUST"
 S MA("ITEMS","QUIT TO MAIN MENU")="D ^TT"
 D GO^TTMENU(.MA)
 G TOP
 Q

ADD
 N CUSTNAME,CUSTCODE
 W !,"RESPOND CUSTOMER CODE "
 R CUSTCODE
 W !,"RESPOND CUSTOMER NAME "
 R CUSTNAME
 S ^TT("CUSTOMER",CUSTCODE)=CUSTNAME
 W !,"RESPOND CUSTOMER POINT OF CONTACT  "
 R ^TT("CUSTOMER",CUSTCODE,"POC")
 W !,"RESPOND CUSTOMER STREET "
 R ^TT("CUSTOMER",CUSTCODE,"STREET")
 W !,"RESPOND CUSTOMER CITY "
 R ^TT("CUSTOMER",CUSTCODE,"CITY")
 W !,"RESPOND CUSTOMER STATE "
 R ^TT("CUSTOMER",CUSTCODE,"STATE")
 W !,"RESPOND CUSTOMER POSTAL CODE "
 R ^TT("CUSTOMER",CUSTCODE,"POSTCODE")
 W !,"RESPOND CUSTOMER EMAIL "
 R ^TT("CUSTOMER",CUSTCODE,"EMAIL")
 W !,"RESPOND CUSTOMER RATE/HOUR "
 R ^TT("CUSTOMER",CUSTCODE,"RATEHOUR")
 W !,"RESPOND CUSTOMER TAX RATE "
 R ^TT("CUSTOMER",CUSTCODE,"TAXRATE")
 S ^TT("COMPANY","LOGIDX")=1
 S ^TT("CUSTOMER",CUSTCODE,"ENTRIDX")=1
 Q

INTGACCT
 N TMP,INVDEB,INVCRED,PMTDEB,PMTCRED,CUSTCODE
 S CUSTCODE=$$CHOOSE^TTCUST(.TMP)
 W !,"CHOOSE ASSET ACCOUNT TO DEBIT ON INVOICE SUBMISSION:",!
 S INVDEB=$$CHOOSE^TTACCOUNT(.TMP)
 W !,"CHOOSE REVENUE/INCOME ACCOUNT TO CREDIT ON INVOICE SUBMISSION:",!
 S INVCRED=$$CHOOSE^TTACCOUNT(.TMP)
 W !,"CHOOSE ASSET ACCOUNT TO DEBIT ON PAYMENT RECEIPT:",!
 S PMTDEB=$$CHOOSE^TTACCOUNT(.TMP)
 W !,"CHOOSE ASSET ACCOUNT TO CREDIT ON PAYMENT RECEIPT:",!
 S PMTCRED=$$CHOOSE^TTACCOUNT(.TMP)
 TS
 S ^TT("CUSTOMER",CUSTCODE,"INVDEB")=INVDEB
 S ^TT("CUSTOMER",CUSTCODE,"INVCRED")=INVCRED
 S ^TT("CUSTOMER",CUSTCODE,"PMTDEB")=PMTDEB
 S ^TT("CUSTOMER",CUSTCODE,"PMTCRED")=PMTCRED
 S ^TT("CUSTOMER",CUSTCODE,"ACCTINTEG")="TRUE"
 TC
 Q
 

SHOW
 N CUSTCODE,CUSTREC,CUSTNAME,CNLEN,CUSTCITY,CUSTSTATE,CUSTZIP
 N CUSTPOC,CUSTSTREET,CUSTMAIL,CUSTTAX,CUSTRATE,ACCTINTEG
 N INVCRED,INVDEB,PMTCRED,PMTDEB,FILE
 N TOTINV,TOTPAID,TOTOWED S (TOTINV,TOTPAID,TOTOWED)=0
 S CUSTCODE=$$CHOOSE^TTCUST(.CUSTREC)
 D OPENTEMP^TTIO(.FILE)
 S CUSTNAME=^TT("CUSTOMER",CUSTCODE)
 S CNLEN=$L(CUSTNAME)
 S CUSTSTREET=^TT("CUSTOMER",CUSTCODE,"STREET")
 S CUSTCITY=^TT("CUSTOMER",CUSTCODE,"CITY")
 S CUSTSTATE=^TT("CUSTOMER",CUSTCODE,"STATE")
 S CUSTZIP=^TT("CUSTOMER",CUSTCODE,"POSTCODE")
 S CUSTPOC=^TT("CUSTOMER",CUSTCODE,"POC")
 S CUSTMAIL=^TT("CUSTOMER",CUSTCODE,"EMAIL")
 S CUSTTAX=^TT("CUSTOMER",CUSTCODE,"TAXRATE")
 S CUSTRATE=^TT("CUSTOMER",CUSTCODE,"RATEHOUR")
 S INVCRED=$G(^TT("CUSTOMER",CUSTCODE,"INVCRED"),"")
 S INVDEB=$G(^TT("CUSTOMER",CUSTCODE,"INVDEB"),"")
 S PMTCRED=$G(^TT("CUSTOMER",CUSTCODE,"PMTCRED"),"")
 S PMTDEB=$G(^TT("CUSTOMER",CUSTCODE,"PMTDEB"),"")
 S ACCTINTEG=$G(^TT("CUSTOMER",CUSTCODE,"ACCTINTEG"),"FALSE")
 W !,!,CUSTNAME,!
 F I=1:1:CNLEN W "="
 W !,!
 W "STREET:    ",CUSTSTREET,?41,"POC:       ",CUSTPOC,!
 W "CITY:      ",CUSTCITY,?41,"EMAIL:     ",CUSTMAIL,!
 W "STATE:     ",CUSTSTATE,?41,"TAX RATE:  ",CUSTTAX,"%",!
 W "ZIP:       ",CUSTZIP,?41,"RATE/HOUR: $",CUSTRATE,!,!
 I ACCTINTEG="TRUE" D
 . W "INVOICING-ACCOUNTING INTEGRATION ENABLED",!
 . W " INVOICES DEBIT:",?41,^TT("ACCT",INVDEB),!
 . W " INVOICES CREDIT:",?41,^TT("ACCT",INVCRED),!
 . W " PAYMENTS DEBIT:",?41,^TT("ACCT",PMTDEB),!
 . W " PAYMENTS CREDIT: ",?41,^TT("ACCT",PMTCRED),!,!
 W "INVOICES",!
 W "========",!,!
 W "#",?8,"DATE",?20,"HOURS",?27,"TOTAL",!
 F I=1:1:80 W "-"
 W !
 N INVIDX S INVIDX=""
 F  S INVIDX=$O(^TT("CUSTOMER",CUSTCODE,"INV",INVIDX)) Q:INVIDX=""  D
 . W INVIDX
 . W ?8,^TT("CUSTOMER",CUSTCODE,"INV",INVIDX,"DATE")
 . W ?20,$J(^TT("CUSTOMER",CUSTCODE,"INV",INVIDX,"HOURS"),5,2)
 . W ?27,"$",$J(^TT("CUSTOMER",CUSTCODE,"INV",INVIDX,"TOTAL"),15,2)
 . W !
 . S TOTINV=TOTINV+^TT("CUSTOMER",CUSTCODE,"INV",INVIDX,"TOTAL")
 W !,"PAYMENTS",!
 W "========",!,!
 W "#",?8,"DATE",?20,"CHECK #",?29,"NMGRT TXBL",?41,"TOTAL",!
 F I=1:1:80 W "-"
 W !
 N PMTIDX S PMTIDX=""
 F  S PMTIDX=$O(^TT("CUSTOMER",CUSTCODE,"PAYMENTS",PMTIDX)) Q:PMTIDX=""  D
 . W PMTIDX
 . W ?8,^TT("CUSTOMER",CUSTCODE,"PAYMENTS",PMTIDX,"DATE")
 . W ?20,^TT("CUSTOMER",CUSTCODE,"PAYMENTS",PMTIDX,"CHKNUM")
 . W ?29,^TT("CUSTOMER",CUSTCODE,"PAYMENTS",PMTIDX,"NMGRTAMOUNT")
 . W ?41,^TT("CUSTOMER",CUSTCODE,"PAYMENTS",PMTIDX,"AMOUNT"),!
 . W "    ",^TT("CUSTOMER",CUSTCODE,"PAYMENTS",PMTIDX,"MEMO"),!
 . S TOTPAID=TOTPAID+^TT("CUSTOMER",CUSTCODE,"PAYMENTS",PMTIDX,"AMOUNT")
 W !,!
 S TOTOWED=TOTINV-TOTPAID
 N PRCPAID S PRCPAID=(TOTPAID*100)/TOTINV
 W ?3,"TOTAL INVOICED:  $",$J(TOTINV,20,2),!
 W ?3,"TOTAL PAID:      $",$J(TOTPAID,20,2),!
 W ?3,"TOTAL OWED:      $",$J(TOTOWED,20,2),!
 W ?3,"PERCENT PAID:     ",$J(PRCPAID,20,2),"%",!
 D CLOSETEMP^TTIO(.FILE)
 D VIEW^TTFILE(FILE,CUSTNAME)
 Q

CHOOSE(CHOSEN)
 N MA,CUSTCODE S CUSTCODE=""
 S MA("TITLE")="CUSTOMER"
 S CUSTCODE=""
 F  S CUSTCODE=$O(^TT("CUSTOMER",CUSTCODE)) Q:CUSTCODE=""  D
 . S MA("ITEMS",^TT("CUSTOMER",CUSTCODE))="S CURRENTCUST="""_CUSTCODE_""""
 D GO^TTMENU(.MA)
 M CHOSEN=^TT("CUSTOMER",CURRENTCUST)
 Q CURRENTCUST
 